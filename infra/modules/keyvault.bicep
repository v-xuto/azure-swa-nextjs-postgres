@description('Name of the Key Vault')
param name string

@description('Location for the Key Vault')
param location string

@description('Tags for the resource')
param tags object = {}

@description('Principal ID to grant Key Vault Secrets User role (SWA managed identity)')
param principalId string

@description('Principal ID of the deployer to grant Key Vault Secrets User role for migrations')
param deployerPrincipalId string

@secure()
@description('PostgreSQL connection string to store as a secret')
param databaseUrl string

resource keyVault 'Microsoft.KeyVault/vaults@2023-07-01' = {
  name: name
  location: location
  tags: tags
  properties: {
    sku: {
      family: 'A'
      name: 'standard'
    }
    tenantId: subscription().tenantId
    enableRbacAuthorization: true
    enableSoftDelete: true
    softDeleteRetentionInDays: 7
  }
}

// Store DATABASE_URL as a Key Vault secret
resource databaseUrlSecret 'Microsoft.KeyVault/vaults/secrets@2023-07-01' = {
  parent: keyVault
  name: 'DATABASE-URL'
  properties: {
    value: databaseUrl
  }
}

// Grant the SWA managed identity "Key Vault Secrets User" role
resource keyVaultSecretUserRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  scope: keyVault
  name: guid(keyVault.id, principalId, '4633458b-17de-408a-b874-0445c86b69e6')
  properties: {
    principalId: principalId
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '4633458b-17de-408a-b874-0445c86b69e6')
    principalType: 'ServicePrincipal'
  }
}

// Grant the deployer "Key Vault Secrets User" role so postprovision migration scripts can read the secret
resource deployerKeyVaultSecretUserRole 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  scope: keyVault
  name: guid(keyVault.id, deployerPrincipalId, '4633458b-17de-408a-b874-0445c86b69e6')
  properties: {
    principalId: deployerPrincipalId
    roleDefinitionId: subscriptionResourceId('Microsoft.Authorization/roleDefinitions', '4633458b-17de-408a-b874-0445c86b69e6')
    principalType: 'User'
  }
}

output name string = keyVault.name
output uri string = keyVault.properties.vaultUri
output id string = keyVault.id
output databaseUrlSecretUri string = databaseUrlSecret.properties.secretUri
